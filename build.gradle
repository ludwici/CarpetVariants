plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    version "$project.minecraft_version-$project.mod_version"

    java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_id,
                    'Specification-Vendor'    : mod_authors,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_authors,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    dependencies {
        implementation 'org.jetbrains:annotations:24.1.0'
    }

    processResources {
        def expandProps = [
                "version": version,
                "group": project.group,
                "java_version": java_version,
                "minecraft_version": minecraft_version,
                "minecraft_version_range": minecraft_version_range,
                "fabric_version": fabric_version,
                "fabric_loader_version": fabric_loader_version,
                "forge_version": forge_version,
                "forge_version_range": forge_version_range,
                "forge_loader_version_range": forge_loader_version_range,
                "mod_id": mod_id,
                "mod_name": mod_name,
                "mod_authors": mod_authors,
                "mod_license": mod_license,
                "mod_url": mod_url,
                "mod_description": project.description,
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version "$project.minecraft_version-$project.mod_version"
                from components.java
            }
        }

        repositories {
            maven {
                url "file://" + System.getenv("local_maven")
            }
        }
    }
}
